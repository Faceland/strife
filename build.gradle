import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "java"
    id "maven"
    id "maven-publish"
    id "wrapper"
}

sourceCompatibility = "1.7"

defaultTasks 'clean', 'licenseFormat', 'check', 'build'

group = "org.nunnerycode"
archivesBaseName = "strife-gradle"
version = "0.0.1-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "http://repo-topplethenun.rhcloud.com/nexus/content/groups/public/"
    }
    maven {
        url "http://nexus.theyeticave.net/content/repositories/pub_releases"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        url "http://repo.comphenix.net/content/groups/public/"
    }
}

dependencies {
    compile "org.nunnerycode:facecore-gradle-all:0.0.3-SNAPSHOT"
    compile "org.nunnerycode:beast-gradle:0.0.1-SNAPSHOT"
    compile "org.nunnerycode:loot-gradle:0.0.1-SNAPSHOT"
}

processResources {
    filter ReplaceTokens, tokens: [
            VERSION: version + "-" + (System.getenv("CIRCLECI") != null ? "circleci" : "compiled") + "-" + (System.getenv("CIRCLE_BUILD_NUM") ?: "0"),
            NAME   : project.name
    ]
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = "strife-gradle"
            from components.java
        }
    }
    repositories {
        maven {
            url "http://repo-topplethenun.rhcloud.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "nunnerycode-repo"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}